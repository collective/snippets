*** Settings ***
Library		CustomSeleniumLibrary.py

*** Keywords ***
Open Browser To Add Page
	Set Selenium Speed		.1
	Set Selenium Implicit Wait 		5
	Open Browser 			${HOME_PAGE}	${BROWSER}
	Maximize Browser Window
	Login Test User

Login Admin
	Click Element	${LOGIN_LINK}
	Input Text		${LOGIN_NAME}	admin
	Input Text		${LOGIN_PASS}	admin
	Click Element	${LOGIN_SUBMIT}

Login Test User 	
	Go To 			${LOGOUT}
	Input Text 		${LOGIN_NAME} 		test2
	Input Text 		${LOGIN_PASS}		password
	Click Element 	${LOGIN_SUBMIT}
	
Logout User
	Go To 			${LOGOUT}
	Go To 			${HOME_PAGE}

Create New Page
	Go To 			${ADD_PAGE}
	Input Text 		title 		${NEW_TITLE}
	Input Text 		description 	${NEW_DESC}
	Select Frame 	${NEW_TEXT_FRAME}
	Input Text 		content 	${NEW_TEXT}
	Unselect Frame
	Click Element 	${EDIT_SAVE}
	Go To 			${LOGOUT}
	Go To 			${HOME_PAGE}
	Login Admin
	Go To 			${EDIT_PAGE}

Check Restricted Access
	Close Snippet Browser
	Click Element 	${EDIT_SAVE}
	Go To 			${LOGOUT}
	Login Test User
	Go To 			${EDIT_PAGE}
	Open Snippet Browser
	New Frame
	Element Should Be Visible 		${BROWSER_BROWSE}
	Click Element	${BROWSER_BROWSE}
	New Frame
	Element Should Be Visible		${TEST_BROWSER_ITEM}
	Element Should Not Be Visible 	${TEST_BROWSER_DELETE}
	Element Should Not Be Visible 	${TEST_BROWSER_EDIT}
	Go To 			${TEST_EDIT_URL}
	Page Should Contain 			Insufficient Privileges
	Go To 			${TEST_DELETE_URL}
	Page Should Contain 			Insufficient Privileges
	Go To 			${TEST_CREATE_URL}
	Page Should Contain 			Insufficient Privileges
	Logout user
	Go To 			${EDIT_PAGE}
	Page Should Not Contain 		${BROWSER_BROWSE}
	Go To 			${HOME_PAGE}
	Login Admin
	Go To 			${EDIT_PAGE}

Create Snippet 		[Arguments]	${TITLE}	${TEXT}
	Click Element	${BROWSER_CREATE}
	Input Text		${CREATE_TITLE}			${TITLE}
	Input Text		${CREATE_DESC}			${TEST_DESC}
	Select Frame 	${CREATE_TEXT_FRAME}
	Input Text		${CREATE_TEXT}			${TEXT}
	New Frame
	Click Element	${CREATE_SAVE}
	Handle Prompts
	Check Preview Set 	${TITLE} 	${TEXT}

Delete Snippet
	Click Element 		${BROWSER_BROWSE}
	New Frame
	Click Element 		${TEST_BROWSER_DELETE}
	Unselect Frame
	Click Element		${DELETE_OK}
	Click Element		${DELETE_OK}
	New Frame
	Click Element 		${TEST_BROWSER_DELETE_2}
	Unselect Frame
	Click Element		${DELETE_OK}
	Click Element		${DELETE_OK}
	Get Newest TinyMCE Window
	Page Should Not Contain Element			${TEST_BROWSER_ITEM}
	Page Should Not Contain Element			${TEST_BROWSER_ITEM_2}
	Click Element		${BROWSER_CANCEL}
	Check Delete
	Go To 				${HOME_PAGE}

Edit Snippet
	Click Element	${BROWSER_BROWSE}
	New Frame
	Click Element 	${TEST_BROWSER_EDIT}
	Select Frame 	${CREATE_TEXT_FRAME}
	Clear Element	${CREATE_TEXT}
	Input Text 		${CREATE_TEXT}			${TEST_TEXT_EDIT}
	New Frame
	Click Element 	${CREATE_SAVE}
	Handle Prompts
	New Frame
	Click Element	${BROWSER_CANCEL}
	Close Snippet Browser
	Check Edit

Insert Snippet
	Click Element	${BROWSER_BROWSE}
	New Frame
	Click Element	${TEST_BROWSER_ITEM}
	Click Element	${BROWSER_SELECT}
	New Frame
	Check Preview Set 	${TEST_TITLE} 	${TEST_TEXT}
	Click Element	${BROWSER_INSERT}
	Unselect Frame
	Select Frame 	${EDIT_TEXT_FRAME}
	Current Frame Contains		${TEST_TEXT}
	Unselect Frame
	Check Change 	${TEST_TEXT}
	Select Frame 	${EDIT_TEXT_FRAME}
	Click Element	${EDIT_SNIPPET}
	New Frame
	Check Preview Set 	${TEST_TITLE} 	${TEST_TEXT}

Preview Snippet
	Click Element		${PREVIEW_OPEN}
	Click Element		${BROWSER_BROWSE}
	New Frame
	Click Element		${TEST_BROWSER_ITEM_2}
	Click Element		${BROWSER_SELECT}
	New Frame
	Click Element		${PREVIEW_BOX}
	Click Element		${PREVIEW_INSERT}
	Current Frame Contains	${TEST_TEXT_2}
	Click Element		${PREVIEW_SAVE}

Open Snippet Browser
	Wait Until Element Is Visible	${BROWSER_OPEN}
	Click Element					${BROWSER_OPEN}
	Wait For New Window
	Get Newest TinyMCE Window

Check Change 	[Arguments] 	${TEXT}
	Click Element 		${EDIT_SAVE}
	Wait Until Element Is Visible 	${CONTAINER}
	Current Frame Contains			${TEXT}
	Go To 		${EDIT_PAGE}

Check Delete
	Go To 				${HOME_PAGE}
	Handle Prompts
	Wait Until Element Is Visible				${CONTAINER}
	Page Should Not Contain 					${TEST_TEXT_2}
	Go To 				${EDIT_PAGE}

Check Edit
	Go To 		${HOME_PAGE}
	Handle Prompts
	Wait Until Element Is Visible	${CONTAINER}
	Current Frame Contains			${TEST_TEXT_EDIT}
	Go To 		${EDIT_PAGE}
	Select Frame 	${EDIT_TEXT_FRAME}
	Click Element 	${EDIT_SNIPPET}
	New Frame
	Check Preview Set 	${TEST_TITLE} 	${TEST_TEXT_EDIT}

Check Preview Set 	[Arguments]	${TITLE} 	${TEXT}
	Element Should Contain 		${INFO_TITLE}		${TITLE}
	Element Should Contain 		${INFO_DESC}		${TEST_DESC}
	Element Should Contain 		${INFO_TEXT}		${TEXT}

Swap Snippets
	Select Frame 	${EDIT_TEXT_FRAME}
	Click Element 	${EDIT_SNIPPET}
	New Frame
	Element Should Be Visible 	${EDIT_REMOVE}
	Click Element	${BROWSER_BROWSE}
	New Frame
	Click Element 	${TEST_BROWSER_ITEM_2}
	Click Element	${BROWSER_SELECT}
	New Frame
	Element Should Not Be Visible 	${EDIT_REMOVE}
	Click Element 	${BROWSER_INSERT}
	Unselect Frame
	Check Change 	${TEST_TEXT_2}

Remove Snippets
	Select Frame 	${EDIT_TEXT_FRAME}
	Click Element 	${EDIT_SNIPPET_2}
	New Frame
	Check Preview Set 	${TEST_TITLE_2}		${TEST_TEXT_2}
	Element Should Be Visible		${EDIT_REMOVE}
	Click Element 	${EDIT_REMOVE}
	Unselect Frame
	Click Element 	${EDIT_REMOVE_OK}
	New Frame
	Element Should Not Be Visible 			${EDIT_REMOVE}
	Click Element	${EDIT_CANCEL}
	Select Frame 	${EDIT_TEXT_FRAME}
	Element Should Not Be Visible	${EDIT_SNIPPET_2}
	Unselect Frame

New Frame
	Unselect Frame
	Get Newest TinyMCE Window

Close Snippet Browser
	Unselect Frame
	Click Element	${BROWSER_CLOSE}

Close Everything
	Go To 			${DELETE_PLUG}${TEST_TITLE}
	Go To 			${DELETE_PLUG}${TEST_TITLE2}
	Go To 			${HOME_PAGE}robottesting/delete_confirmation
	Click Element 	${DELETE_BUTTON}
	Close Browser
